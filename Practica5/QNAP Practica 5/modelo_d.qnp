/DECLARE/ QUEUE CPU,DISC,TERMINAL;
          REAL PROB(1)=(7.);
          REAL TTR1,TOTAL,WORK,SCPU,SDISCO,DCPU,DDISCO,DEMANDA,XO,ZTIME,RTIME;
          INTEGER I,N1,V1,V2,PSATURACION;
/STATION/ NAME=CPU;
&         SCHED=PS;
          SERVICE=EXP(0.03);
          TRANSIT=DISC,PROB,TERMINAL,1;
/STATION/ NAME=DISC;
          TRANSIT=CPU;
          SERVICE=EXP(0.1);
/STATION/ NAME=TERMINAL;
          TYPE=INFINITE;
          INIT=N1;
          SERVICE=EXP(8.);
          TRANSIT=CPU;
/CONTROL/ CLASS=ALL QUEUE;
/EXEC/    FOR N1:=1 STEP 1 UNTIL 30 DO
          BEGIN
            PRINT;
            PRINT("NUMERO DE USUARIOS=",N1);
            SOLVE;
            PRINT("");
            PRINT("APARTADO a");
            PRINT("");
            V2:=PROB(1);
            V1:=V2+1;
            PRINT("RAZÓN DE VISITA CPU (Vcpu)            =",V1);
            PRINT("RAZÓN DE VISITA DISC (Vdisco)         =",V2);
            PRINT("");
            SCPU:=MSERVICE(CPU);
            SDISCO:=MSERVICE(DISC);
            PRINT("TIEMPO DE SERVICIO CPU (Scpu)         =",SCPU);
            PRINT("TIEMPO DE SERVICIO DISC (Sdisco)      =",SDISCO);
            PRINT("");
            DCPU:=V1*SCPU;
            DDISCO:=V2*SDISCO;
            PRINT("*DEMANDA CPU (Dcpu)*                  =",DCPU);
            PRINT("*DEMANDA DISC (Ddisco)*               =",DDISCO);
            PRINT("");
            DEMANDA:=DCPU+DDISCO;
            PRINT("*DEMANDA DEL SISTEMA (D)*             =",DEMANDA);
            PRINT("");
            PRINT("*CUELLO DE BOTELLA (Db)*              =",MAX(DCPU,DDISCO));
            PRINT("");
            ZTIME:=MRESPONSE(TERMINAL);
            PRINT("TIEMPO DE REFLEXIÓN (Z)               =",ZTIME);
            PRINT("");
            PSATURACION:=INTROUND((DEMANDA+ZTIME)/MAX(DCPU,DDISCO));
            PRINT("*PUNTO DE SATURACIÓN (N*)*            =",PSATURACION);
            PRINT("");
            PRINT("APARTADO b");
            PRINT("");
            PRINT("TRABAJOS TOTALES (N)                  =",N1);
            XO:=MTHRUPUT(TERMINAL);
            PRINT("PRODUCTIVIDAD DEL SISTEMA (Xo)        =",XO);
            PRINT("");
            RTIME:=(N1/XO)-ZTIME;
            PRINT("*TIEMPO DE RESPUESTA DEL SISTEMA (R)* =",RTIME);
            PRINT("");
            PRINT("*TIEMPO TOTAL DEL SISTEMA (R+Z)*      =",RTIME+ZTIME);
            PRINT("");
            PRINT("*USUARIOS TRABAJANDO*                 =",N1-MCUSTNB(TERMINAL));
            PRINT("*USUARIOS REFLEXIONANDO*              =",MCUSTNB(TERMINAL));
          END;
/END/
